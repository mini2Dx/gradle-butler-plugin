import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.9.0'
	}
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'

if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	apply plugin: "io.codearte.nexus-staging"

	nexusStaging {
		username = ossrhUser
		password = ossrhPassword
	}
}

group = 'org.mini2Dx'
version = '1.1.3'
description = 'Gradle plugin for the itch.io butler command line tool'

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
	maven { url "https://mini2dx.org/maven/content/repositories/thirdparty" }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

dependencies {
	compile gradleApi()
	compile localGroovy()
	compile 'de.undercouch:gradle-download-task:3.1.2'

	testCompile 'junit:junit:4.11'
	testCompile gradleTestKit()
}

// Maven Central publication
if(project.hasProperty('ossrhUser')) {
	signing {
		sign configurations.archives
	}
	
	uploadArchives {
		repositories {
			mavenDeployer {
				// POM signature
				beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
				// Target repository
				repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
					authentication(userName: ossrhUser, password: ossrhPassword)
				}
				pom.project {
					name project.name
					description project.description
					packaging 'jar'
					url 'https://github.com/mini2Dx/gradle-butler-plugin'

					scm {
						connection 'scm:git:https://github.com/mini2Dx/gradle-butler-plugin.git'
						developerConnection 'scm:git:git@github.com:mini2Dx/gradle-butler-plugin.git'
						url 'https://github.com/mini2Dx/gradle-butler-plugin.git'
					}

					licenses {
						license {
							name 'The MIT License (MIT)'
							url 'https://opensource.org/licenses/MIT'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id = 'tomcashman'
							name = 'Thomas Cashman'
							email = 'thomascashman404@gmail.com'
						}
					}
				}
			}
		}
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}