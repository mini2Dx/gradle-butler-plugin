
buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://plugins.gradle.org/m2/' }
	}
	dependencies {
		classpath 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2'
		classpath 'de.marcphilipp.gradle:nexus-publish-plugin:0.4.0'
	}
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'groovy'
apply plugin: 'maven-publish'
apply plugin: 'signing'

if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	apply plugin: "io.codearte.nexus-staging"
	apply plugin: "de.marcphilipp.nexus-publish"

	nexusStaging {
		packageGroup = 'org.mini2Dx'
		username = ossrhUser
		password = ossrhPassword
		numberOfRetries = 30
		delayBetweenRetriesInMillis = 5000
	}
}

group = 'org.mini2Dx'
version = '2.1.0'
description = 'Gradle plugin for the itch.io butler command line tool'

repositories {
	mavenLocal()
	mavenCentral()
	maven { url "https://mini2dx.org/maven/content/repositories/thirdparty" }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

dependencies {
	implementation gradleApi()
	implementation localGroovy()
	implementation 'de.undercouch:gradle-download-task:3.1.2'

	testImplementation 'junit:junit:4.11'
	testImplementation gradleTestKit()
}

// Maven Central publication
if(project.hasProperty('ossrhUser') && project.hasProperty("release")) {
	publishing {
		publications {
			mavenJava(MavenPublication) {
				groupId = project.group

				afterEvaluate {
					from components.java
					artifact sourcesJar
					artifact javadocJar
					artifactId = artifactId.toLowerCase()
				}
				pom {
					groupId = project.group
					name = project.name.toLowerCase()
					afterEvaluate {
						description = project.description
					}
					packaging = 'jar'
					url = 'https://github.com/mini2Dx/gradle-butler-plugin'

					licenses {
						license {
							name = 'Apache License 2.0'
							url = 'https://opensource.org/licenses/Apache-2.0'
							distribution = 'repo'
						}
					}
					developers {
						developer {
							id = 'tomcashman'
							name = 'Thomas Cashman'
							email = 'thomascashman404@gmail.com'
						}
					}
					scm {
						connection = 'scm:git:https://github.com/mini2Dx/gradle-butler-plugin.git'
						developerConnection = 'scm:git:git@github.com:mini2Dx/gradle-butler-plugin.git'
						url = 'https://github.com/mini2Dx/gradle-butler-plugin.git'
					}
				}
			}
		}
		repositories {
			maven {
				name = "GitHubPackages"
				url = uri("https://maven.pkg.github.com/mini2Dx/gradle-butler-plugin")
				credentials {
					username = project.getProperty('gpr.user')
					password = project.getProperty('gpr.key')
				}
			}
		}

		nexusPublishing {
			connectTimeout = Duration.ofMinutes(15)
			clientTimeout = Duration.ofMinutes(15)
			repositories {
				sonatype {
					packageGroup = rootProject.nexusStaging.packageGroup
					stagingProfileId = rootProject.nexusStaging.stagingProfileId
					username = project.getProperty('ossrhUser')
					password = project.getProperty('ossrhPassword')
				}
			}
		}

		signing {
			sign publishing.publications
		}
	}
}
